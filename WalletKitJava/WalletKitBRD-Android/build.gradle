apply plugin: 'com.android.library'
apply from: '../gradle/publish.gradle'

def activateAndroid = gradle.startParameter.taskNames.any {  it.contains("-Android") }
def disableIdeaCheck = activateAndroid || property("disableIdeaCheck") == "true"
def ideaActive = !disableIdeaCheck && System.getProperty("idea.active") == "true"

android {
    compileSdkVersion androidConfig.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionCode 1
        versionName version

        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
    }

    lintOptions {
        quiet false
        explainIssues true
        abortOnError true
        ignoreWarnings false
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            if (!ideaActive) {
                java {
                    srcDirs = [project(':WalletKitBRD').javaMainSrcDir]
                }
            }
        }
        androidTest {
            if (!ideaActive) {
                java {
                    srcDirs += [project(':WalletKitBRD').javaTestSrcDir]
                }
            }
        }
    }
}

dependencies {
    api project(':WalletKit')
    api deps.guavaAndroid

    implementation project(':WalletKitNative-Android')
    implementation deps.androidxAppcompat
    implementation deps.jacksonAnnotations
    implementation deps.jacksonCore
    implementation deps.jacksonDatabind
    implementation deps.jacksonDatabindGuava

    testImplementation deps.junit

    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.androidxTestEspresso
}
